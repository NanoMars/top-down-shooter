# player_section.gd
extends Control

var controller_id: int = -1
var player_id: int = -1
var loaded: bool = false

var start_button_held: bool = true
var leave_button_held: bool = true

var spring_velocity: float = 0.0

@export var spring_strength: float = 0.9
@export var spring_dampening: float = 0.2

@export var player_label: Label
@export var press_start_label: Label

func _ready() -> void:
	PlayerManager.player_id_changed.connect(_on_player_id_changed)

func _process(_delta: float) -> void:
	if controller_id == -1 or player_id == -1:
		return
	
	if not loaded and controller_id != -1 and player_id != -1:
		loaded = true
		update_labels()
		

	if Input.is_joy_button_pressed(controller_id, JOY_BUTTON_A) and player_id == 1:
		if not start_button_held:
			print("start button pressed for controller ", controller_id, "and player ", player_id)
			start_button_held = true
	else:
		start_button_held = false


	if Input.is_joy_button_pressed(controller_id, JOY_BUTTON_B):
		if not leave_button_held:
			print("leave button pressed for controller ", controller_id, "and player ", player_id)
			PlayerManager.leave_player(controller_id)
			queue_free()
			leave_button_held = true
	else:
		leave_button_held = false

func _physics_process(delta: float) -> void:
	spring_velocity += (1 - size_flags_stretch_ratio) * spring_strength * delta
	spring_velocity -= spring_velocity * spring_dampening * delta
	size_flags_stretch_ratio += spring_velocity * delta

func _on_player_id_changed(cid: int, pid: int) -> void:
	if cid == self.controller_id:
		player_id = pid
		update_labels()

func update_labels() -> void:
	player_label.text = "Player " + str(player_id)
	
	if player_id == 1:
		press_start_label.text = "Press A to start or B to leave"
	else:
		press_start_label.text = "Press B to leave"
